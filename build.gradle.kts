import java.text.SimpleDateFormat
import java.util.*

plugins {
    id("eclipse")
    id("idea")
    id("maven-publish")
    id("net.minecraftforge.gradle").version("[6.0,6.2)")
    id("org.parchmentmc.librarian.forgegradle").version("1.+")
    id("org.spongepowered.mixin")
}

val minecraftVersion: String by extra
val minecraftVersionRange: String by extra
val loaderVersionRange: String by extra
val forgeVersionRange: String by extra
val modVersion: String by extra
val modGroupId: String by extra
val mod_id: String by extra
val modAuthors: String by extra
val modDescription: String by extra
val modLicense: String by extra
val modName: String by extra
val parchmentChannel: String by extra
val parchmentVersion: String by extra
val forgeVersion: String by extra
val lodestoneVersion: String by extra
val jeiVersion: String by extra
val curiosVersion: String by extra
val mixinVersion: String by extra
val modJavaVersion: String by extra

version = "$minecraftVersion-$modVersion"
if (System.getenv("BUILD_NUMBER") != null) {
    version = "$minecraftVersion-$modVersion.${System.getenv("BUILD_NUMBER")}"
}
group = modGroupId

val baseArchivesName = mod_id
base {
    archivesName.set(baseArchivesName)
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(modJavaVersion))
    }
}

mixin {
    add(sourceSets.main.get(), "${mod_id}.refmap.json")
    config("lodestonejs.mixins.json")
}

minecraft {
    mappings(parchmentChannel, parchmentVersion)

    accessTransformer(file("src/main/resources/META-INF/accesstransformer.cfg"))

    copyIdeResources.set(true)
    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory(project.file("run"))

            property("forge.logging.markers", "REGISTRIES")
            property("forge.logging.console.level", "debug")

            mods {
                create("${mod_id}") {
                    source(sourceSets.main.get())
                }
            }
        }

        create("client") {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property("forge.enabledGameTestNamespaces", mod_id)
            arg("-mixin.config=" + mod_id + ".mixins.json")
        }

        create("server") {
            property("forge.enabledGameTestNamespaces", mod_id)
            args("--nogui")
            arg("-mixin.config=" + mod_id + ".mixins.json")
        }

        create("data") {
            // example of overriding the workingDirectory set in configureEach above
            workingDirectory(project.file("run-data"))

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args(
                    "--mod",
                    mod_id,
                    "--all",
                    "--output",
                    file("src/generated/resources/"),
                    "--existing",
                    file("src/main/resources/")
            )
        }
    }
}

// Include resources generated by data generators.
sourceSets {
    main {
        resources.srcDir("src/generated/resources")
    }
}

repositories {
    mavenCentral()
    maven {
        name = "Curios maven"
        url = uri("https://maven.theillusivec4.top/")
        content {
            includeGroupByRegex("top\\.theillusivec4.*")
        }
    }
    maven {
        name = "JEI maven"
        url = uri("https://dvs1.progwml6.com/files/maven")
    }
    maven {
        name = "tterrag maven"
        url = uri("https://maven.tterrag.com/")
    }
    maven {
        name = "BlameJared maven"
        url = uri("https://maven.blamejared.com/")
    }
    maven {
        name = "Curse Maven"
        url = uri("https://cursemaven.com")
        content {
            includeGroup("curse.maven")
        }
    }
    maven {
        name = "Architectury maven"
        url = uri("https://maven.architectury.dev")
        content {
            includeGroup("dev.architectury")
        }
    }
    maven {
        name = "Saps maven"
        url = uri("https://maven.saps.dev/minecraft")
        content {
            includeGroup("dev.latvian.mods")
        }
    }
}

dependencies {
    minecraft("net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}")

    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor("org.spongepowered:mixin:${mixinVersion}:processor")
    }

    // JEI Dependency
    compileOnly(fg.deobf("mezz.jei:jei-${minecraftVersion}-forge-api:${jeiVersion}"))
    compileOnly(fg.deobf("mezz.jei:jei-${minecraftVersion}-common-api:${jeiVersion}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${minecraftVersion}-forge:${jeiVersion}"))

    // Curios dependency
    compileOnly(fg.deobf("top.theillusivec4.curios:curios-forge:${curiosVersion}:api"))
    runtimeOnly(fg.deobf("top.theillusivec4.curios:curios-forge:${curiosVersion}"))

    // Lodestone dependency
    implementation(fg.deobf("team.lodestar.lodestone:lodestone:${minecraftVersion}-${lodestoneVersion}"))

    // KubeJS dependency
    implementation(fg.deobf("dev.latvian.mods:kubejs-forge:2001.6.4-build.120"))
    implementation(fg.deobf("dev.latvian.mods:rhino-forge:2001.2.2-build.18"))

    // Architechtury dependency
    implementation(fg.deobf("dev.architectury:architectury-forge:9.1.13"))

    // ProbeJS
    runtimeOnly(fg.deobf("curse.maven:probejs-585406:5227399"))
}

tasks.withType<ProcessResources> {
    inputs.property("version", version)

    filesMatching(listOf("META-INF/mods.toml", "pack.mcmeta")) {
        expand(
                mapOf(
                        "forgeVersionRange" to forgeVersionRange,
                        "loaderVersionRange" to loaderVersionRange,
                        "minecraftVersion" to minecraftVersion,
                        "minecraftVersionRange" to minecraftVersionRange,
                        "modAuthors" to modAuthors,
                        "modDescription" to modDescription,
                        "mod_id" to mod_id,
                        "modJavaVersion" to modJavaVersion,
                        "modName" to modName,
                        "modVersion" to version,
                        "modLicense" to modLicense
                )
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType<Jar> {
    val now = SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssZ").format(Date())
    manifest {
        attributes(mapOf(
                "Specification-Title" to modName,
                "Specification-Vendor" to modAuthors,
                "Specification-Version" to '1',
                "Implementation-Title" to modName,
                "Implementation-Version" to version,
                "Implementation-Vendor" to modAuthors,
                "Implementation-Timestamp" to now,
        ))
    }
    finalizedBy("reobfJar")
}

publishing {
    publications {
        register<MavenPublication>("mavenJava") {
            artifactId = baseArchivesName
            from(components["java"])
            fg.component(this)
        }
    }
    repositories {
        maven {
            url = uri("file://${System.getenv("local_maven")}")
        }
    }
}

idea {
    module {
        for (fileName in listOf("run", "out", "logs")) {
            excludeDirs.add(file(fileName))
        }
    }
}

tasks.withType<JavaCompile> {
    options.encoding = "UTF-8"
}